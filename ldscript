/* Default linker script, for normal executables */
OUTPUT_FORMAT("elf32-powerpc", "elf32-powerpc",
	      "elf32-powerpc")
OUTPUT_ARCH(powerpc:common)
SECTIONS
{
  .text :
  {
    __text_start__ = .;
    *(.text)
    *(init)
    *(INIT)
    *(SORT(.CRT*))
    __text_end__ = .;
    . = ALIGN(4096);
  }
  .data :
  {
    *(.data)
    *(.data2)
    *(.rdata)
    *(.sdata)
    . = ALIGN(4096);
  }
  .pdata :
  {
     *(.pdata*)
     *(.rodata*)
     *(.got2)
     *(.eh_frame)
     . = ALIGN(4096);
  }
  .tls :
  {
     *(SORT(.tls*))
  }
  .idata :
  {
     *(.idata$2)
     . = ALIGN(16);
     SORT(*)(.idata$3)
     . = ALIGN(16);
     LONG(0); LONG(0); LONG(0); LONG(0); LONG(0);
     SORT(*)(.idata$4)
     . = ALIGN(16);
     SORT(*)(.idata$5)
     . = ALIGN(16);
     SORT(*)(.idata$6)
     . = ALIGN(16);
     SORT(*)(.idata$7)
     . = ALIGN(4096);
  }
  .edata :
  {
    *(.edata)
    . = ALIGN(4096);
  }
  .bss :
  {
    *(.sbss)
    *(.bss)
    *(COMMON)
    . = ALIGN(4096);
  }
  .rsrc :
  {
    *(.rsrc)
    . = ALIGN(4096);
  }
}
